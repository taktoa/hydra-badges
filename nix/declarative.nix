{ nixpkgs, prsJSON }:

with { pkgs = import nixpkgs {}; };

with {
  mkJobset = (
    { enabled          ? 1,
      hidden           ? false,
      description      ? "",
      checkinterval    ? 30,
      schedulingshares ? 10,
      enableemail      ? false,
      emailoverride    ? false,
      keepnr           ? 3,
      nixexprinput,
      nixexprpath,
      inputs
    }@args:

    {
      inherit enabled hidden description;
      inherit emailoverride enableemail;
      inherit checkinterval schedulingshares keepnr;
      inherit nixexprinput nixexprpath inputs;
    } // args);

  mkFetchGithub = value: {
    inherit value;
    type = "git";
    emailresponsible = false;
  };
};

with pkgs.lib;

with rec {
  mkProjectJobset = (
    { url ? null, branch ? "master", ... }@args:

    mkJobset ((removeAttrs args ["url" "branch"]) // {
      nixexprpath = "release.nix";
      nixexprinput = "src";
      inputs = {
        src = mkFetchGithub (
          if url != null
          then "${url} ${branch}"
          else "https://github.com/taktoa/hydra-badges ${branch}");
      };
    }));

  primaryJobsets = {
    "master" = mkProjectJobset {
      branch = "master";
      description = "taktoa/hydra-badges master";
    };
    
    "staging" = mkProjectJobset {
      branch = "staging";
      description = "taktoa/hydra-badges staging";
    };
  };

  prData = builtins.fromJSON (builtins.readFile prsJSON);

  makePr = num: info: (
    with { inherit (info.head) repo; };
    {
      name = "pr-${num}";
      value = mkProjectJobset {
        description = "PR ${num}: ${info.title}";
        url = "https://github.com/${repo.owner.login}/${repo.name}.git";
        branch = info.head.ref;
      };
    });

  pullRequests = listToAttrs (mapAttrsToList makePr prData);

  jobsetsAttrs = pullRequests // primaryJobsets;
};

{
  jobsets = pkgs.writeText "spec.json" (builtins.toJSON jobsetsAttrs);
}
